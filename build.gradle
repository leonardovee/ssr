plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'

	id 'com.github.node-gradle.node' version '7.0.1'
}

group = 'dev.leonardovee'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

node {
	version = '18.17.1'
	npmVersion = '9.6.7'
	download = true
	workDir = file("${project.buildDir}/nodejs")
	nodeProjectDir = file("${project.projectDir}/src/frontend")
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('npmBuild', NpmTask) {
	args = ['run', 'build']
	dependsOn npmInstall
}

tasks.register('copyFrontend', Copy) {
	from 'src/frontend/dist'
	into 'src/main/resources/static'
	dependsOn npmBuild
}

tasks.named('processResources') {
	dependsOn copyFrontend
}
